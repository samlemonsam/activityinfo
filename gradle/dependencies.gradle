// build a map of the dependency artifacts to use.  Allows centralized definition of the version of artifacts to
// use.  In that respect it serves a role similar to <dependencyManagement> in Maven
ext {

    guavaVersion = '15.0'
    rebarVersion = '2.4.4'
    gwtVersion = '2.6.0-rc3'
    jerseyVersion = '1.17'
    gaeVersion = '1.9.6'
    jacksonVersion = '1.9.2'
    cucumberVersion = '1.2.2'
    libraries = [

        guava:              "com.google.guava:guava:${guavaVersion}",
        gson:               "com.google.code.gson:gson:2.2.4",
        guice:              "com.google.inject:guice:3.0",
        guiceServlet:       "com.google.inject.extensions:guice-servlet:3.0",
        guiceMultibindings: "com.google.inject.extensions:guice-multibindings:3.0",
            
        appengine: [
            api:            "com.google.appengine:appengine-api-1.0-sdk:${gaeVersion}",
            sdk:            "com.google.appengine:appengine-java-sdk:${gaeVersion}",
            gcs:            "com.google.appengine.tools:appengine-gcs-client:0.4.4",  
            testing:        "com.google.appengine:appengine-testing:${gaeVersion}",
            stubs:          "com.google.appengine:appengine-api-stubs:${gaeVersion}",
            export: [
                itext:      'com.bedatadriven.appengine.export:appengine-itext:0.1.1',
                awt:        'com.bedatadriven.appengine.export:appengine-awt:0.1.1',
                ppt:        'com.bedatadriven.appengine.export:appengine-ppt:0.1.1',
                jcharts:    'com.bedatadriven.appengine.export:appengine-jcharts:0.1.1',
                emf:        'com.bedatadriven.appengine.export:appengine-emf:0.1.1',
            ]
        ],
            
        metrics: [
            core:           'io.dropwizard.metrics:metrics-core:3.1.1'
        ],
            
        appengineMetrics:   'com.bedatadriven.appengine:appengine-metrics:0.3-SNAPSHOT',
            
        gwt:[
            user:           "com.google.gwt:gwt-user:${gwtVersion}", 
            servlet:        "com.google.gwt:gwt-servlet:${gwtVersion}",
            dev:            "com.google.gwt:gwt-dev:${gwtVersion}",
            i18nServer:     "com.teklabs.gwt-i18n-server:gwt-i18n-server:0.7-bedatadriven2",
            dnd:            'com.allen-sauer.gwt.dnd:gwt-dnd:3.3.0',
            leaflet:        'org.discotools.gwt.leaflet:gwtl-core:0.3-SNAPSHOT',
            guice:          "com.google.gwt.inject:gin:2.1.2",
            guava:          "com.google.guava:guava-gwt:${guavaVersion}",
            gson:           "gson-gwt:gson-gwt:1.0",
            gears:          'com.google.gwt.google-apis:gwt-gears:1.2.1',

        ],
            
        gxt:                'com.extjs:gxt:2.3.1-gwt26-bdd-9',
            
        rebar: [
            time:           "com.bedatadriven.rebar:rebar-time:${rebarVersion}",
            asyncFun:       "com.bedatadriven.rebar:rebar-asyncfun:${rebarVersion}",
            sql:            "com.bedatadriven.rebar:rebar-sql:${rebarVersion}",
            sync:           "com.bedatadriven.rebar:rebar-sync:${rebarVersion}",
            appcache:       "com.bedatadriven.rebar:rebar-appcache:${rebarVersion}",
            style:          "com.bedatadriven.rebar:rebar-style:${rebarVersion}"
        ],
            
        jersey: [
            core:           "com.sun.jersey:jersey-core:${jerseyVersion}",
            server:         "com.sun.jersey:jersey-server:${jerseyVersion}",
            client:         "com.sun.jersey:jersey-client:${jerseyVersion}",
            guice:          "com.sun.jersey.contribs:jersey-guice:${jerseyVersion}",
            json:           "com.sun.jersey:jersey-json:${jerseyVersion}",
            multipart:      "com.sun.jersey.contribs:jersey-multipart:${jerseyVersion}"
        ],
            
        jackson: [
            geojson:        'com.bedatadriven:jackson-geojson:0.7'
        ],
            
        slf4j: [
            api:            'org.slf4j:slf4j-api:1.6.1',
            impl:           'org.slf4j:slf4j-jdk14:1.6.1'
        ],
            
        hibernate: [
            entitymanager:  'org.hibernate:hibernate-entitymanager:4.2.8.Final',
            spatial:        'org.hibernate:hibernate-spatial:4.0',
            validator:      'org.hibernate:hibernate-validator:4.3.1.Final'
        ],
        liquibase:          'org.liquibase:liquibase-core:2.0.2-bdd',

        commons: [
            fileupload:     'commons-fileupload:commons-fileupload:1.2.2',
            io:             'commons-io:commons-io:1.3.2'    
        ],

        jts:                'com.vividsolutions:jts:1.12',
        
        junit:              'junit:junit:4.11',
        hamcrest:           'org.hamcrest:hamcrest-library:1.3',
        easymock:           'org.easymock:easymock:3.1',
        dbunit:             'org.dbunit:dbunit:2.4.8',
        c3p0:               'com.mchange:c3p0:0.9.5',
            
        freemarker:         'org.freemarker:freemarker-gae:2.3.20',
            
        jdbc: [
            mysql:          'mysql:mysql-connector-java:5.1.27',
            sqlite:         'org.xerial:sqlite-jdbc:3.6.17'
        ],
            
            
        bcrypt:             'org.mindrot.bcrypt:bcrypt:0.3',
            
        google: [
            apiClient:      'com.google.api-client:google-api-client:1.19.0',
        ],            
        prettytime:         'org.ocpsoft.prettytime:prettytime:2.1.2.Final' ,
        jsr305:             'com.google.code.findbugs:jsr305:3.0.0',
            
        cucumber: [
            java:           "info.cukes:cucumber-java:${cucumberVersion}",
            junit:          "info.cukes:cucumber-junit:${cucumberVersion}",
            guice:          "info.cukes:cucumber-guice:${cucumberVersion}"
        ]
    ]
}

subprojects {

    configurations.all {

        // Interferes with GWT-dev
        exclude group:'xerces', module: 'xercesImpl'     

        // Required by Hibernate Spatial but not needed
        exclude group:'postgresql', module: 'postgresql' 
        exclude group:'org.postgresql', module: 'postgresql-jdbc'
        exclude group:'org.postgis', module: 'postgis-jdbc'


        // Conflicts with hibernate's version of the API
        exclude group:'javax.persistence', module: 'persistence-api'
        exclude group:'javax.transaction', module: 'jta'

        exclude group:'com.sun.xml.bind', module:'jaxb-impl'

        exclude group:'xml-apis', module: 'xml-apis'
        exclude module: 'guava-jdk5'
        
        // Exclude CGLIB which is actually repackaged by guice
        exclude group: 'org.sonatype.sisu.inject', module:'cglib'
        
        resolutionStrategy {

            force libraries.guava
            force libraries.gwt.values()
            force libraries.jts


            // Resolve dependencies as Maven would have for the moment
            // to avoid introducing changes with the new build script

            force 'com.google.api-client:google-api-client:1.19.0'
            force 'org.json:json:20090211'
            

            eachDependency { DependencyResolveDetails details ->
                if (details.requested.group == 'org.codehaus.jackson') {
                    details.useVersion jacksonVersion
                } else if(details.requested.group == 'com.google.appengine') {
                    details.useVersion gaeVersion
                }
            }
        }
    }

}