
/**
 * Define common configuration properties and 
 * apply cross-project processes.
 */
apply from: "./gradle/libraries.gradle"
apply from: "./gradle/version.gradle"
apply from: "./gradle/jacoco.gradle"
apply from: './gradle/database.gradle'
apply from: "./gradle/environment.gradle"


task wrapper(type: Wrapper) {
  gradleVersion = '2.5'
}

allprojects  {
  apply plugin: 'maven'
  apply plugin: 'idea'
  group = 'org.activityinfo'
  
}

idea {
  project {
    jdkName = '1.7'
    languageLevel = '1.7'
    vcs = 'Git'
  }
}


subprojects {

  apply plugin: 'java'


  sourceCompatibility = 1.7
  targetCompatibility = 1.7


  // This is required to stop javac from attempting to compile
  // java sources included in .jar files intended for GWT
  tasks.withType(JavaCompile) {
    options.compilerArgs += ["-implicit:none", "-sourcepath", ""]
    options.encoding = 'UTF-8'
  }

  repositories {
    mavenLocal()
    maven { url "https://nexus.bedatadriven.com/content/groups/public" }
    maven { url "https://plugins.gradle.org/m2/" }
  }

  dependencies {
    compile libraries.slf4j.api
    compile libraries.slf4j.impl

    testCompile libraries.junit
    testCompile libraries.hamcrest
    testCompile libraries.easymock
  }
}


/**
 * Define the dependencies of *this* build script itself.
 *
 * These are the plugins and other libraries required to evaluate
 * the *.gradle files themselves.
 */
buildscript {
  repositories {
    maven {
      url 'http://dl.bintray.com/steffenschaefer/maven'
    }
    maven {
      url "https://plugins.gradle.org/m2/"
    }
    jcenter()
  }
  dependencies {
    classpath 'de.richsource.gradle.plugins:gwt-gradle-plugin:0.6'
    classpath 'com.bmuschko:gradle-nexus-plugin:2.3'
    classpath 'com.moowork.gradle:gradle-grunt-plugin:0.12'
    classpath "com.moowork.gradle:gradle-node-plugin:0.12"
    classpath "gradle.plugin.org.kravemir.gradle.sass:plugin:1.2.3"
  }
}
